#define TAK

piece base, hull1, hull2_1, hull2, hull2_2,
	  turret_130_1, sleeve_130_1, gun_130_1, flare_130_1, turret_130_2, sleeve_130_2, gun_130_2, flare_130_2,
	  turret_130_3, sleeve_130_3, gun_130_3, flare_130_3, turret_130_4, sleeve_130_4, gun_130_4, flare_130_4,
	  turret_76_1, sleeve_76_1, gun_76_1, flare_76_1, turret_76_2, sleeve_76_2, gun_76_2, flare_76_2,
	  turret_37_1, sleeve_37_1, gun_37_1, flare_37_1, turret_37_2, sleeve_37_2, gun_37_2, flare_37_2,
	  turret_37_3, sleeve_37_3, gun_37_3, flare_37_3, turret_37_4, sleeve_37_4, gun_37_4, flare_37_4,
	  mg_turret1, mg_gun1, mg_flare1, mg_turret2, mg_gun2, mg_flare2,
	  twin_mg_1, guns_tmg1, flare_tmg1_1, flare_tmg1_2, twin_mg_2, guns_tmg2, flare_tmg2_1, flare_tmg2_2,
	  t_launcher1, t1_flare_1, t1_flare_2, t1_flare_3, t_launcher2, t2_flare_1, t2_flare_2, t2_flare_3,
	  radar, exhaust, wake1, wake2, wake3, wake4;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define MAIN_TURRET_TURN		<10>
#define MAIN_TURRET_PITCH		<15>

#define AA76_TURRET_TURN		<20>
#define AA76_TURRET_PITCH		<25>

#define AA37_TURRET_TURN		<30>
#define AA37_TURRET_PITCH		<30>

#define MG_TURRET_TURN			<30>
#define MG_TURRET_PITCH			<45>

#define TL_TURRET_TURN			<10>

//*********************Recoil
#define MAIN_GUN_RECOIL			[2]
#define MAIN_GUN_RECOIL_SPEED	[10]
#define MAIN_GUN_BACK_SPEED		[0.9]

#define AA76_GUN_RECOIL			[1]
#define AA76_GUN_RECOIL_SPEED	[10]
#define AA76_GUN_BACK_SPEED		[0.7]

#define AA37_GUN_RECOIL			[0.4]
#define AA37_GUN_RECOIL_SPEED	[10]
#define AA37_GUN_BACK_SPEED		[2]

//*********************Death anim
#define HULL_MOVE_DIST			[10]
#define HULL_MOVE_SPEED			[1]
#define HULL_LIST_SPEED			<1>
#define HULL_LIST_ANGLE			<10>

//********************Engine smoke
#define ENGINE_SMOKE_PIECE	exhaust
#define ENGINE_SMOKE_PERIOD	200

//********************Radar
#define RADAR_SPIN_SPEED		<30>

#define RESTORE_PERIOD		2500

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32
#define SIG_AIM5			64
#define SIG_AIM6			128
#define SIG_AIM7			256
#define SIG_AIM8			512
#define SIG_AIM9			1024
#define SIG_AIM10			2048
#define SIG_AIM11			4096
#define SIG_AIM12			8192
#define SIG_AIM13			16384
#define SIG_AIM14			32768
#define SIG_AIM15			65536
#define SIG_AIM16			131072

#define ALL_WEAPON_SIGS		SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_AIM5|SIG_AIM6|SIG_AIM7|SIG_AIM8|SIG_AIM9|SIG_AIM10|SIG_AIM11|SIG_AIM12|SIG_AIM13|SIG_AIM14|SIG_AIM15|SIG_AIM16

#define SMOKEPIECE	hull1

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var					CurrentSpeed, bMoving, tmg1Flare, tmg2Flare, tl1Flare, tl2Flare;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			emit-sfx SFXTYPE_BLACKSMOKE from ENGINE_SMOKE_PIECE;
			sleep ENGINE_SMOKE_PERIOD;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn turret_130_3 to y-axis <180> now;
	turn turret_130_4 to y-axis <180> now;
	turn turret_76_1 to y-axis <180> now;
	turn turret_76_2 to y-axis <180> now;
	turn turret_37_3 to y-axis <180> now;
	turn turret_37_4 to y-axis <180> now;
	tmg1Flare=TRUE;
	tmg2Flare=TRUE;
	tl1Flare=1;
	tl2Flare=1;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

HideFlares()
{
	hide flare_130_1;
	hide flare_130_2;
	hide flare_130_3;
	hide flare_130_4;
	hide flare_76_1;
	hide flare_76_2;
	hide flare_37_1;
	hide flare_37_2;
	hide flare_37_3;
	hide flare_37_4;
	hide mg_flare1;
	hide mg_flare2;
	hide flare_tmg1_1;
	hide flare_tmg1_2;
	hide flare_tmg2_1;
	hide flare_tmg2_2;
}

Create()
{
	start-script HideFlares();
	bMoving = FALSE;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
}

SweetSpot(piecenum)
{
	piecenum = hull1;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		emit-sfx 2 from wake4;
		sleep 300;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

StopCatcher()
{
	var newXZ, oldXZ, newHeading, oldHeading;
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while(TRUE)
	{
		sleep 300;
		newXZ = get PIECE_XZ(base);
		newHeading = get HEADING;
		if((oldXZ == newXZ) && (oldHeading == newHeading))
		{
			call-script StopMoving();
		}
		oldXZ = newXZ;
		oldHeading = newHeading;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	start-script StopCatcher();
	bMoving = TRUE;
}

Activate()
{
	spin radar around y-axis speed RADAR_SPIN_SPEED;
}

Deactivate()
{
	stop-spin radar around y-axis;
}

RestoreTurret()
{
	set-signal-mask SIG_DEATH|ALL_WEAPON_SIGS;
	sleep RESTORE_PERIOD;

	turn turret_130_1 to y-axis <0> speed MAIN_TURRET_TURN;
	turn sleeve_130_1 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn turret_130_2 to y-axis <0> speed MAIN_TURRET_TURN;
	turn sleeve_130_2 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn turret_130_3 to y-axis <180> speed MAIN_TURRET_TURN;
	turn sleeve_130_3 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn turret_130_4 to y-axis <180> speed MAIN_TURRET_TURN;
	turn sleeve_130_4 to x-axis <0> speed MAIN_TURRET_PITCH;

	turn turret_76_1 to y-axis <180> speed AA76_TURRET_TURN;
	turn sleeve_76_1 to x-axis <0> speed AA76_TURRET_PITCH;
	turn turret_76_2 to y-axis <180> speed AA76_TURRET_TURN;
	turn sleeve_76_2 to x-axis <0> speed AA76_TURRET_PITCH;

	turn turret_37_1 to y-axis <0> speed AA37_TURRET_TURN;
	turn sleeve_37_1 to x-axis <0> speed AA37_TURRET_PITCH;
	turn turret_37_2 to y-axis <0> speed AA37_TURRET_TURN;
	turn sleeve_37_2 to x-axis <0> speed AA37_TURRET_PITCH;
	turn turret_37_3 to y-axis <180> speed AA37_TURRET_TURN;
	turn sleeve_37_3 to x-axis <0> speed AA37_TURRET_PITCH;
	turn turret_37_4 to y-axis <180> speed AA37_TURRET_TURN;
	turn sleeve_37_4 to x-axis <0> speed AA37_TURRET_PITCH;

	turn mg_turret1 to y-axis <0> speed MG_TURRET_TURN;
	turn mg_gun1 to x-axis <0> speed MG_TURRET_PITCH;
	turn mg_turret2 to y-axis <0> speed MG_TURRET_TURN;
	turn mg_gun2 to x-axis <0> speed MG_TURRET_PITCH;

	turn twin_mg_1 to y-axis <0> speed MG_TURRET_TURN;
	turn guns_tmg1 to x-axis <0> speed MG_TURRET_PITCH;
	turn twin_mg_2 to y-axis <0> speed MG_TURRET_TURN;
	turn guns_tmg2 to x-axis <0> speed MG_TURRET_PITCH;
	
	turn t_launcher1 to y-axis <0> speed TL_TURRET_TURN;
	turn t_launcher2 to y-axis <0> speed TL_TURRET_TURN;
}

QueryWeapon1(piecenum)
{
	piecenum = flare_130_1;
}

AimFromWeapon1(piecenum)
{
	piecenum = turret_130_1;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	turn turret_130_1 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve_130_1 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret_130_1 around y-axis;
	wait-for-turn sleeve_130_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	show flare_130_1;
	move gun_130_1 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_130_1 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare_130_1;
}

QueryWeapon2(piecenum)
{
	piecenum = flare_130_2;
}

AimFromWeapon2(piecenum)
{
	piecenum = turret_130_2;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	turn turret_130_2 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve_130_2 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret_130_2 around y-axis;
	wait-for-turn sleeve_130_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	show flare_130_2;
	move gun_130_2 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_130_2 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare_130_2;
}

QueryWeapon3(piecenum)
{
	piecenum = flare_130_3;
}

AimFromWeapon3(piecenum)
{
	piecenum = turret_130_3;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	turn turret_130_3 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve_130_3 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret_130_3 around y-axis;
	wait-for-turn sleeve_130_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	show flare_130_3;
	move gun_130_3 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_130_3 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare_130_3;
}

QueryWeapon4(piecenum)
{
	piecenum = flare_130_4;
}

AimFromWeapon4(piecenum)
{
	piecenum = turret_130_4;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	turn turret_130_4 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve_130_4 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret_130_4 around y-axis;
	wait-for-turn sleeve_130_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	show flare_130_4;
	move gun_130_4 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_130_4 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare_130_4;
}

QueryWeapon5(piecenum)
{
	piecenum = flare_76_1;
}

AimFromWeapon5(piecenum)
{
	piecenum = turret_76_1;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM5;
	set-signal-mask SIG_AIM5|SIG_DEATH;
	turn turret_76_1 to y-axis heading speed AA76_TURRET_TURN;
	turn sleeve_76_1 to x-axis (0-pitch) speed AA76_TURRET_PITCH;
	wait-for-turn turret_76_1 around y-axis;
	wait-for-turn sleeve_76_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon5()
{
	show flare_76_1;
	move gun_76_1 to z-axis (0-AA76_GUN_RECOIL) speed AA76_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_76_1 to z-axis [0] speed AA76_GUN_BACK_SPEED;
	hide flare_76_1;
}

QueryWeapon6(piecenum)
{
	piecenum = flare_76_2;
}

AimFromWeapon6(piecenum)
{
	piecenum = turret_76_2;
}

AimWeapon6(heading, pitch)
{
	signal SIG_AIM6;
	set-signal-mask SIG_AIM6|SIG_DEATH;
	turn turret_76_2 to y-axis heading speed AA76_TURRET_TURN;
	turn sleeve_76_2 to x-axis (0-pitch) speed AA76_TURRET_PITCH;
	wait-for-turn turret_76_2 around y-axis;
	wait-for-turn sleeve_76_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon6()
{
	show flare_76_2;
	move gun_76_2 to z-axis (0-AA76_GUN_RECOIL) speed AA76_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_76_2 to z-axis [0] speed AA76_GUN_BACK_SPEED;
	hide flare_76_2;
}

QueryWeapon7(piecenum)
{
	piecenum = flare_37_1;
}

AimFromWeapon7(piecenum)
{
	piecenum = turret_37_1;
}

AimWeapon7(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	turn turret_37_1 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_1 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_1 around y-axis;
	wait-for-turn sleeve_37_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon7()
{
	show flare_37_1;
	move gun_37_1 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_37_1 to z-axis [0] speed AA37_GUN_BACK_SPEED;
	hide flare_37_1;
}

QueryWeapon8(piecenum)
{
	piecenum = flare_37_2;
}

AimFromWeapon8(piecenum)
{
	piecenum = turret_37_2;
}

AimWeapon8(heading, pitch)
{
	signal SIG_AIM8;
	set-signal-mask SIG_AIM8|SIG_DEATH;
	turn turret_37_2 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_2 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_2 around y-axis;
	wait-for-turn sleeve_37_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon8()
{
	show flare_37_2;
	move gun_37_2 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_37_2 to z-axis [0] speed AA37_GUN_BACK_SPEED;
	hide flare_37_2;
}

QueryWeapon9(piecenum)
{
	piecenum = flare_37_3;
}

AimFromWeapon9(piecenum)
{
	piecenum = turret_37_3;
}

AimWeapon9(heading, pitch)
{
	signal SIG_AIM9;
	set-signal-mask SIG_AIM9|SIG_DEATH;
	turn turret_37_3 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_3 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_3 around y-axis;
	wait-for-turn sleeve_37_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon9()
{
	show flare_37_3;
	move gun_37_3 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_37_3 to z-axis [0] speed AA37_GUN_BACK_SPEED;
	hide flare_37_3;
}

QueryWeapon10(piecenum)
{
	piecenum = flare_37_4;
}

AimFromWeapon10(piecenum)
{
	piecenum = turret_37_4;
}

AimWeapon10(heading, pitch)
{
	signal SIG_AIM10;
	set-signal-mask SIG_AIM10|SIG_DEATH;
	turn turret_37_4 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_4 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_4 around y-axis;
	wait-for-turn sleeve_37_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon10()
{
	show flare_37_4;
	move gun_37_4 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move gun_37_4 to z-axis [0] speed AA37_GUN_BACK_SPEED;
	hide flare_37_4;
}

QueryWeapon11(piecenum)
{
	if(tmg1Flare)
	{
		piecenum = flare_tmg1_1;
	} else {
		piecenum = flare_tmg1_2;
	}
}

AimFromWeapon11(piecenum)
{
	piecenum = twin_mg_1;
}

AimWeapon11(heading, pitch)
{
	signal SIG_AIM11;
	set-signal-mask SIG_AIM11|SIG_DEATH;
	turn twin_mg_1 to y-axis heading speed MG_TURRET_TURN;
	turn guns_tmg1 to x-axis (0-pitch) speed MG_TURRET_PITCH;
	wait-for-turn twin_mg_1 around y-axis;
	wait-for-turn guns_tmg1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon11()
{
	if(tmg1Flare)
	{
		show flare_tmg1_1;
		sleep 200;
		hide flare_tmg1_1;
	} else {
		show flare_tmg1_2;
		sleep 200;
		hide flare_tmg1_2;
	}
	tmg1Flare = !tmg1Flare;
}

QueryWeapon12(piecenum)
{
	if(tmg2Flare)
	{
		piecenum = flare_tmg2_1;
	} else {
		piecenum = flare_tmg2_2;
	}
}

AimFromWeapon12(piecenum)
{
	piecenum = twin_mg_2;
}

AimWeapon12(heading, pitch)
{
	signal SIG_AIM12;
	set-signal-mask SIG_AIM12|SIG_DEATH;
	turn twin_mg_2 to y-axis heading speed MG_TURRET_TURN;
	turn guns_tmg2 to x-axis (0-pitch) speed MG_TURRET_PITCH;
	wait-for-turn twin_mg_2 around y-axis;
	wait-for-turn guns_tmg2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon12()
{
	if(tmg2Flare)
	{
		show flare_tmg2_1;
		sleep 200;
		hide flare_tmg2_1;
	} else {
		show flare_tmg2_2;
		sleep 200;
		hide flare_tmg2_2;
	}
	tmg2Flare = !tmg2Flare;
}

QueryWeapon13(piecenum)
{
	piecenum = mg_flare1;
}

AimFromWeapon13(piecenum)
{
	piecenum = mg_turret1;
}

AimWeapon13(heading, pitch)
{
	signal SIG_AIM13;
	set-signal-mask SIG_AIM13|SIG_DEATH;
	turn mg_turret1 to y-axis heading speed MG_TURRET_TURN;
	turn mg_gun1 to x-axis (0-pitch) speed MG_TURRET_PITCH;
	wait-for-turn mg_turret1 around y-axis;
	wait-for-turn mg_gun1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon13()
{
	show mg_flare1;
	sleep 200;
	hide mg_flare1;
}

QueryWeapon14(piecenum)
{
	piecenum = mg_flare2;
}

AimFromWeapon14(piecenum)
{
	piecenum = mg_turret2;
}

AimWeapon14(heading, pitch)
{
	signal SIG_AIM14;
	set-signal-mask SIG_AIM14|SIG_DEATH;
	turn mg_turret2 to y-axis heading speed MG_TURRET_TURN;
	turn mg_gun2 to x-axis (0-pitch) speed MG_TURRET_PITCH;
	wait-for-turn mg_turret2 around y-axis;
	wait-for-turn mg_gun2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon14()
{
	show mg_flare2;
	sleep 200;
	hide mg_flare2;
}

QueryWeapon15(piecenum)
{
	if(tl1Flare == 1)
	{
		piecenum = t1_flare_1;
	}
	if(tl1Flare == 2)
	{
		piecenum = t1_flare_2;
	}
	if(tl1Flare == 3)
	{
		piecenum = t1_flare_3;
	}
}

AimFromWeapon15(piecenum)
{
	piecenum = t_launcher1;
}

AimWeapon15(heading, pitch)
{
	signal SIG_AIM15;
	set-signal-mask SIG_AIM15|SIG_DEATH;
	turn t_launcher1 to y-axis heading speed TL_TURRET_TURN;
	wait-for-turn t_launcher1 around y-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon15()
{
	if(tl1Flare == 1)
	{
		show t1_flare_1;
		sleep 200;
		hide t1_flare_1;
	}
	if(tl1Flare == 2)
	{
		show t1_flare_2;
		sleep 200;
		hide t1_flare_2;
	}
	if(tl1Flare == 3)
	{
		show t1_flare_3;
		sleep 200;
		hide t1_flare_3;
	}
	tl1Flare = tl1Flare + 1;
	if(tl1Flare>3)
	{
		tl1Flare = 1;
	}
}

QueryWeapon16(piecenum)
{
	if(tl2Flare == 1)
	{
		piecenum = t2_flare_1;
	}
	if(tl2Flare == 2)
	{
		piecenum = t2_flare_2;
	}
	if(tl2Flare == 3)
	{
		piecenum = t2_flare_3;
	}
}

AimFromWeapon16(piecenum)
{
	piecenum = t_launcher2;
}

AimWeapon16(heading, pitch)
{
	signal SIG_AIM16;
	set-signal-mask SIG_AIM16|SIG_DEATH;
	turn t_launcher2 to y-axis heading speed TL_TURRET_TURN;
	wait-for-turn t_launcher2 around y-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon16()
{
	if(tl2Flare == 1)
	{
		show t2_flare_1;
		sleep 200;
		hide t2_flare_1;
	}
	if(tl2Flare == 2)
	{
		show t2_flare_2;
		sleep 200;
		hide t2_flare_2;
	}
	if(tl2Flare == 3)
	{
		show t2_flare_3;
		sleep 200;
		hide t2_flare_3;
	}
	tl2Flare = tl2Flare + 1;
	if(tl2Flare>3)
	{
		tl2Flare = 1;
	}
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<99)
	{
		corpsetype = 1;
		move hull1 to z-axis HULL_MOVE_DIST speed HULL_MOVE_SPEED;
		move hull2 to z-axis (0-HULL_MOVE_DIST) speed HULL_MOVE_SPEED;
		turn hull1 to x-axis (0-HULL_LIST_ANGLE) speed HULL_LIST_SPEED;
		//turn hull2_1 to x-axis (0-HULL_LIST_ANGLE) speed HULL_LIST_SPEED;
		turn hull2 to x-axis HULL_LIST_ANGLE speed HULL_LIST_SPEED;
		//turn hull2_2 to x-axis HULL_LIST_ANGLE speed HULL_LIST_SPEED;
		wait-for-move hull1 along z-axis;
		wait-for-turn hull1 around x-axis;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull1 type		SHATTER|BITMAP1;
	explode hull2 type		SHATTER|BITMAP1;
	return (TRUE);
}