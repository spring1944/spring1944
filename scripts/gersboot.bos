#define TAK

piece base, hull, hull2, mount20_1, sleeve20_1, barrel20_1, flare20_1, mount20_2, sleeve20_2, barrel20_2, flare20_2,
	  torp1, torp2, door1, door2, spare1, spare2,
	  wake1, wake2, wake3;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define AA20_TURRET_TURN		<45>
#define AA20_TURRET_PITCH		<45>

//*********************Recoil
#define AA20_GUN_RECOIL			[0.4]
#define AA20_GUN_RECOIL_SPEED	[10]
#define AA20_GUN_BACK_SPEED		[0.5]

//*********************Death anim
#define HULL_LIST_SPEED		<30>
#define HULL_LIST_ANGLE		<-180>

//*********************Torpedo bay doors
#define OPEN_ANGLE			<120>
#define OPEN_SPEED			<450>

//*********************Cruise mode
#define CRUISE_MODE_ANGLE	<-4>
#define CRUISE_MODE_RAISE_SPEED	<1>
#define CRUISE_MODE_START_DIVIDER	4
#define CRUISE_MODE_START_MULTIPLIER	3
#define CRUISE_MODE_PERIOD	250

#define RESTORE_PERIOD		2500

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32

#define SMOKEPIECE	hull

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var					MaxSpeed, CruiseTriggerSpeed, bMoving;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			sleep 500;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn mount20_2 to y-axis <180> now;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

CruiseControl()
{
	var CurrentSpeed;
	set-signal-mask SIG_DEATH;
	while(TRUE)
	{
		CurrentSpeed = get CURRENT_SPEED;
		if(CurrentSpeed > CruiseTriggerSpeed)
		{
			turn hull to x-axis CRUISE_MODE_ANGLE speed CRUISE_MODE_RAISE_SPEED;		
		} else
		{
			turn hull to x-axis <0> speed CRUISE_MODE_RAISE_SPEED;
		}
		sleep CRUISE_MODE_PERIOD;
	}
}

Create()
{
	bMoving = FALSE;
	MaxSpeed = get MAX_SPEED;
	CruiseTriggerSpeed = MaxSpeed * CRUISE_MODE_START_MULTIPLIER / CRUISE_MODE_START_DIVIDER;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
	start-script CruiseControl();
}

SweetSpot(piecenum)
{
	piecenum = hull;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		sleep 300;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	bMoving = TRUE;
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

RestoreTurret()
{
	set-signal-mask SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_DEATH;
	sleep RESTORE_PERIOD;

	turn mount20_1 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve20_1 to x-axis <0> speed AA20_TURRET_PITCH;
	turn mount20_2 to y-axis <180> speed AA20_TURRET_TURN;
	turn sleeve20_2 to x-axis <0> speed AA20_TURRET_PITCH;
	turn door1 to y-axis <0> speed OPEN_SPEED;
	turn door2 to y-axis <0> speed OPEN_SPEED;
}

OpenDoors()
{
	turn door1 to y-axis OPEN_ANGLE speed OPEN_SPEED;
	turn door2 to y-axis (0-OPEN_ANGLE) speed OPEN_SPEED;
	wait-for-turn door1 around y-axis;
}

QueryWeapon1(piecenum)
{
	piecenum = torp1;
}

AimFromWeapon1(piecenum)
{
	piecenum = torp1;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	call-script OpenDoors();
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	sleep 200;
}

QueryWeapon2(piecenum)
{
	piecenum = torp2;
}

AimFromWeapon2(piecenum)
{
	piecenum = torp2;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	call-script OpenDoors();
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	sleep 200;
}

QueryWeapon3(piecenum)
{
	piecenum = flare20_1;
}

AimFromWeapon3(piecenum)
{
	piecenum = mount20_1;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	turn mount20_1 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve20_1 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn mount20_1 around y-axis;
	wait-for-turn sleeve20_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	show flare20_1;
	move barrel20_1 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel20_1 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare20_1;
}

QueryWeapon4(piecenum)
{
	piecenum = flare20_2;
}

AimFromWeapon4(piecenum)
{
	piecenum = mount20_2;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	turn mount20_2 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve20_2 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn mount20_2 around y-axis;
	wait-for-turn sleeve20_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	show flare20_2;
	move barrel20_2 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel20_2 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare20_2;
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<99)
	{
		corpsetype = 1;
		turn hull to z-axis HULL_LIST_ANGLE speed HULL_LIST_SPEED;
		wait-for-turn hull around z-axis;
		explode hull type		BITMAPONLY|BITMAP1;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull type		SHATTER|BITMAP1;
	explode spare1 type 	SHATTER;
	explode spare2 type		SHATTER;
	return (TRUE);
}