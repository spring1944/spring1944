#include "constants.h"

piece	base, hull, turret, sleeve, sleeve2, gun, flare, mg_ring, mg, mg_flare, exhaust,
		front_wheels, rear_wheels, roller1, roller2, roller3, roller4, roller5, roller6, roller7, roller8, roller9, roller10, roller11,
		tracks, wake;


static-var  restore_delay, rock, aim2, reloading, mainHead, bMoving, bFloating, treadnum, gunaiming, MGAiming,
			WaterSpeed, LandSpeed;

// water mobility def
#define WATER_SPEED_MULTIPLIER		1
#define WATER_SPEED_DIVIDER			3

#define FLOAT_DEPTH					[-4]

// Signal definitions
#define SIG_MOVE	1
#define SIG_AIM		2 // Main Cannon
#define SIG_AIM_2	4 // Coax MG
#define SIG_AIM_3	8 // AA MG

// definitions for SmokeUnit_GPL
#define SMOKEPIECE1	hull
#define SMOKEPIECE2 turret
#define SMOKEPIECE3 sleeve
#define SMOKEPIECE4 exhaust

#include "SmokeUnit_GPL.h" // Thanks again to Argh

//hiding FX emit points
#define FX_HIDE\
	hide flare;\
	hide mg_flare;

// definitions for RockUnit_Set
#define ROCK_SPEED	<50> // heavier units should rock less
#define RESTORE_SPEED	<20>
#define ROCKVAR		rock

#include "RockUnit_Set.h"

// definitions for wheels
#define WHEEL_SPIN		<600>
#define WHEEL_ACCEL		<150>

// definitions for turret movement
#define TURR_TRAVERSE	<15>
#define TURR_ELEVATE	<7>
#define AA_TRAVERSE		<225>
#define AA_ELEVATE		<150>
#define COAX_ARC		<3>

// definitions for weapons behaviour
#define RELOAD		3500 // time in milliseconds between shots
#define RECOIL		[-2] // distance to recoil the gun
#define RETURN_SPEED	[10] // speed at which the barrel returns

// definitions for HitWeapId_S44
#define ARMOUR		4275
#include "HitWeapId_S44.h"

//effects
#define placeholder_EFFECT01 1024+1
#define placeholder_EFFECT02 1024+2
#define placeholder_EFFECT03 1024+3
#define placeholder_EFFECT04 1024+4
#define placeholder_EFFECT05 1024+5
#define placeholder_EFFECT06 1024+6
#define MG_MUZZLEFLASH 1024+7
#define MEDIUM_MUZZLEFLASH 1024+8
#define MEDIUM_MUZZLEDUST 1024+9

// tracers
#define MEDIUM_TRACER 	2048+3

#include "amphib.h"

// when it'll have some texture to show on tracks, then this will be needed
treads() 
{ 
/*   while( TRUE ) 
   { 
        if(bMoving) 
           { 
              if(treadnum == 0) 
              {             
              hide tracks2; 
              show tracks1; 
              } 
            if(treadnum == 1) 
            { 
            hide tracks1; 
            show tracks2; 
			}
            treadnum = !treadnum; 
         } 
      sleep 90; 
   } */
}

Create()
{
	LandSpeed = get MAX_SPEED;
	WaterSpeed = LandSpeed * WATER_SPEED_MULTIPLIER / WATER_SPEED_DIVIDER;
	turn mg_ring to y-axis <180> now;
	FX_HIDE
	// init vars
	rock = FALSE;
	aim2 = FALSE;
	reloading = FALSE;
	treadnum=0;
	// start continuous scripts
	start-script SmokeUnit_GPL();
	start-script treads();
	start-script WaterCheck();
}

StartMoving()
{
	signal SIG_MOVE;
	bMoving = TRUE;

	spin front_wheels around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin rear_wheels around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller1 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller2 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller3 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller4 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller5 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller6 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller7 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller8 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller9 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller10 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
 	spin roller11 around x-axis speed WHEEL_SPIN accelerate WHEEL_ACCEL;
}

StopMoving()
{
	signal SIG_MOVE;
	bMoving = FALSE;

	stop-spin front_wheels around x-axis;
 	stop-spin rear_wheels around x-axis;
	stop-spin roller1 around x-axis;
	stop-spin roller2 around x-axis;
	stop-spin roller3 around x-axis;
	stop-spin roller4 around x-axis;
	stop-spin roller5 around x-axis;
	stop-spin roller6 around x-axis;
	stop-spin roller7 around x-axis;
	stop-spin roller8 around x-axis;
	stop-spin roller9 around x-axis;
	stop-spin roller10 around x-axis;
	stop-spin roller11 around x-axis;
}

SetMaxReloadTime(longestReloadTime)
{
	restore_delay = longestReloadTime * 2;
}

RestoreAfterDelay()
{
	sleep restore_delay;
	turn turret to y-axis <0.000000> speed TURR_TRAVERSE;
	turn sleeve to x-axis <0.000000> speed TURR_ELEVATE;
	turn mg_ring to y-axis 180 speed AA_TRAVERSE;
	turn mg to x-axis 0 speed AA_ELEVATE;
}

Reload()
{
	reloading = TRUE;
	sleep RELOAD;
	reloading = FALSE;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

// Weapon 1 - Main cannon, HE
QueryWeapon1(piecenum)
{
	piecenum = flare;
}

AimFromWeapon1(piecenum)
{
	piecenum = turret;
}

AimWeapon1(heading, pitch)
{
	if (aim2) return 0; // don't aim weap1 if aiming AP
	
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	GunAiming=1;
	mainHead = heading;
	turn turret to y-axis heading speed TURR_TRAVERSE;
	turn sleeve to x-axis 0 - pitch speed TURR_ELEVATE;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	
	start-script RestoreAfterDelay();
	if (reloading) return 0; // aim first, then pause if the shot cannot fire
	GunAiming=0;
	return 1;
}

FireWeapon1()
{
	rock = TRUE; // firing a heavy weapon
	
	// TODO: Add to SFX
	// Do SFX
	//emit-sfx 1  from flare;
	emit-sfx MEDIUM_MUZZLEFLASH from flare;
	emit-sfx MEDIUM_MUZZLEDUST from flare;
	// barrel recoil
	move gun to z-axis RECOIL now;
	sleep 200;
	move gun to z-axis 0 speed RETURN_SPEED;
	
	rock = FALSE;
	start-script Reload(); // start reload timer after firing
	return 0;
}

// Weapon 2 - AA MG
QueryWeapon2 (piecenum)
	{
	piecenum = mg_flare;
	}

AimFromWeapon2 (piecenum)
	{
	piecenum = mg_ring;
	}

AimWeapon2(heading, pitch)
	{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;

	// thanks to Fang and zwzsg for this!
	turn mg_ring to y-axis heading speed AA_TRAVERSE; 
	turn mg to x-axis 0 - pitch speed AA_ELEVATE; 
	wait-for-turn mg_ring around y-axis; 
	wait-for-turn mg around x-axis;
	
	start-script RestoreAfterDelay();
	return (1);
	}

FireWeapon2()
	{
	rock = FALSE;
	
	// TODO: nice SFX
	emit-sfx MEDIUM_TRACER from mg_flare;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	sleep 109;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	sleep 109;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	sleep 109;
	emit-sfx MEDIUM_TRACER from mg_flare;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	sleep 109;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	sleep 109;
	emit-sfx MG_MUZZLEFLASH from mg_flare;
	emit-sfx MEDIUM_TRACER from mg_flare;
	sleep 109;
	
	return(0);
	}

AimFromWeapon3(piecenum) //bulletproof effect
{
	piecenum = base;
}

QueryWeapon3(piecenum)
{
	piecenum = base;
}

AimWeapon3(heading, pitch)
{
return (1);
}

FireWeapon3()
{
return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		corpsetype = 1;
		return (0);
	}
	if( severity <= 250 )
	{
		corpsetype = 1;
	}
	if(severity <= 1000)
	{
		corpsetype = 2;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL;
	}
	corpsetype = 2;
	explode turret type EXPLODE_ON_HIT | FIRE | FALL;
}

